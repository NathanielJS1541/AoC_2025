=LET(
    hashSeparated, TEXTSPLIT(TEXTJOIN("#", FALSE, A1:A1175), , "##"),
    ranges, NUMBERVALUE(TEXTSPLIT(CONCAT(INDEX(hashSeparated, 1)), "-", "#")),
    availableIDs, NUMBERVALUE(TEXTSPLIT(CONCAT(INDEX(hashSeparated, 2)), "#")),
    sortedRanges, SORTBY(ranges, BYROW(ranges, LAMBDA(row, INDEX(row, , 1)))),
    MINIMISE_RANGES, LAMBDA(ME,s,e,list,
        IF(ROWS(list) > 1,
            LET(
                next_s, INDEX(list, 1, 1),
                next_e, INDEX(list, 1, 2),
                IF(next_s <= e + 1,
                    ME(ME, s, MAX(e, next_e), DROP(list, 1)),
                    VSTACK(HSTACK(s, e), ME(ME, next_s, next_e, DROP(list, 1)))
                )
            ),
        VSTACK(HSTACK(s, e),list)
        )
    ),
    minimisedRanges, DROP(MINIMISE_RANGES(MINIMISE_RANGES, -1, -1, sortedRanges),1),
    IN_RANGE, LAMBDA(x,range,
        ABS(SUM(SIGN(range - x))) < 2
    ),
    X_IN_RANGES, LAMBDA(x,ranges,
        LET(
        X_IN_RANGE, LAMBDA(range,
            IN_RANGE(x, range)
        ),
        IF(OR(BYROW(ranges, X_IN_RANGE)),1,0)
        )
    ),
    XS_IN_MINIMISED_RANGES, LAMBDA(X,
        X_IN_RANGES(X,minimisedRanges)
    ),
    SUM(MAP(availableIDs, XS_IN_MINIMISED_RANGES))
)